//------------------------------------------------------------------------------
// Random util. things.
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Misc.

// Check if 'p' is satisfied with all elements of 'as'.
//  all::(a->bool)->(a list)->bool
let all p as = AND_list (map p as);

// Check if 'p' is satisfied with any element of 'as'.
//  any::(a->bool)->(a list)->bool
let any p as = OR_list (map p as);

// Non-infix version of (==) because I can't figure out how to write that.
//  equal::a->a->bool
let equal a b = a == b;

// Constant function.
//  const::a->b->a
let const a b = a;

// Flip the arguments of a function.
//  flip::(a->b->c)->b->a->c
let flip f a b = f b a;

// "Curry" a function.
//  curry::((a->b->c)->(a#b)->c
let curry f (a,b) = f a b;

// Un"curry" a function.
//  uncurry::((a#b)->c)->a->b->c
let uncurry f a b = f (a,b);

//------------------------------------------------------------------------------
// Pairs

// Apply 'f' to the first element of pair '(a,b)'.
//  first::(a->c)->(a#b)->(c#b)
let first f (a,b) = (f a,b);

// Apply 'f' to the second element of a pair '(a,b)'.
//  second::(b->c)->(a#b)->(a#c)
let second f (a,b) = (a,f b);

//------------------------------------------------------------------------------
// List

// Drops the first element of a list.
//  butfirst::(a list)->(a list)
let butfirst (x:xs) = xs;

// Infinite list of repeated applications of 'f' to 'x'.
//     iterate::(a->a)->a->(a list)
letrec iterate f x = x : iterate f (f x);

// Fold (rigth-to-left) a list 'xs' with 'f' into 'b'.
//  ixlist::(a->int->b->b)->(a list)->b->b
let ixlist f xs b = snd (itlist (\x. \(ix,s). (ix-1,f x ix s)) xs (length xs,b));

// Modify the 'n'th element of a list 'xs' with 'f'.
//  modify::(a->a)->int->(a list)->(a list)
let modify f n xs = ixlist (\x. \ix. \ys. n == ix => f x : ys | x : ys) xs [];

//------------------------------------------------------------------------------
// String

// Drops the first 'n' characters from the string 's'.
//  string_butfirstn::string->int->string
let string_butfirstn s n = el (n+1) (iterate (string_tl) s);

// Short-hand for 'string_tl'.
//  string_butfirst::string->string
let string_butfirst = string_tl;

// Short-hand for 'string_butlastn 1'.
//  string_butlast::string->string
let string_butlast s = string_butlastn s 1;

//------------------------------------------------------------------------------
// Vector/Matrix (lists/list of lists).

// Set the 'n'th element of a list 'x' to 'v'.
//  list_update::a->int->(a vec)->(a vec)
let list_update v i = modify (const v) i;

// Set the '(i,j)'th element of matrix 'mat' to 'v'.
//  mat_update::a->(int#int)->(a mat)->(a mat)
let mat_update v (i,j) = modify (list_update v j) i;

//------------------------------------------------------------------------------
// Ports/Wires.

lettype vector =
          CONSTANT {value::int}
        | VARIABLE {name::string}
        | VECTOR   {name::string} {indices::int list}
;

// Check if two port references are equal.
//  port_eq::vector->vector->bool
let vec_eq (CONSTANT a)  (CONSTANT b)  = a == b
/\  vec_eq (VECTOR a ix) (VECTOR b jx) = a == b AND all (curry equal) (zip ix jx)
/\  vec_eq _ _ = F
;

// ...
//  parse_actual::string->vector
let parse_actual p =
        let parse_constant s = CONSTANT (s2int (string_butfirstn p 2)) in
        let parse_variable s = VARIABLE s in
        let parse_vector ss  =
                let name_of  s = hd (str_split s "[") in
                let index_of s = s2int (string_butlast (el 2 (str_split s "["))) in
                VECTOR (name_of (hd ss)) (map index_of ss)
        in
        str_is_prefix "0b" p => parse_constant p |
        str_is_substr "["  p => parse_vector (md_expand_vector p) |
        parse_variable p
;

// ...
//  parse_actuals::(string#(string list) list)->(string#(vector list) list)
let parse_actuals = map (second (map parse_actual));

//------------------------------------------------------------------------------
// Pexlif.

// Feth a 'PEXLIF's attribute list.
//  attributes_of::pexlif->(string#string list)
let attributes_of (PINST _ as _ _ _ _ _) = as;

// Fetch the "fingerprint" attribute. If none exists, returns 0.
//  find_fingerprint::(string#string list)->int
let find_fingerprint as = find_first0 (\(n,_). n == "fingerprint") as;

// Ill named Short-hand for "find_fingerprint . attributes_of".
//  fp_of::pexlif->int
let fp_of p = find_fingerprint (attributes_of p);

//------------------------------------------------------------------------------
// Fingerprinting.
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Prime numbers.

// todo: replace with built in generator.
let list_of_primes = [
      2,      3,      5,      7,     11,     13,     17,     19,     23,     29,
     31,     37,     41,     43,     47,     53,     59,     61,     67,     71,
     73,     79,     83,     89,     97,    101,    103,    107,    109,    113,
    127,    131,    137,    139,    149,    151,    157,    163,    167,    173,
    179,    181,    191,    193,    197,    199,    211,    223,    227,    229,
    233,    239,    241,    251,    257,    263,    269,    271,    277,    281,
    283,    293,    307,    311,    313,    317,    331,    337,    347,    349,
    353,    359,    367,    373,    379,    383,    389,    397,    401,    409,
    419,    421,    431,    433,    439,    443,    449,    457,    461,    463,
    467,    479,    487,    491,    499,    503,    509,    521,    523,    541
  ];

// note: we cap the integers so that they fit in a 64-bit word.
let roof = ipow 2 63 - 1;

// ...
//  prime::int->int->int
let prime n v = (v * el n list_of_primes) % roof;

// ...
//  prime_list::(a->int)->int->(a list)->int
let prime_list f = flip (ixlist (\a.\i.\s. prime i (f a) + s));

//------------------------------------------------------------------------------
// Gen. of a fingerprint.

// Gen. a fingerprint for 'MEM'.
let
    fp_mem (MEM as l ds) = prime 0 as + prime 1 l + prime 2 ds
;

// Gen. a fingerprint for 'WEXPR'.
letrec
    fp_w (W_X s)		= prime 0 s
/\  fp_w (W_CONST s i)		= prime 1 s + prime 2 i
/\  fp_w (W_NAMED_CONST _ s i)	= prime 3 s + prime 4 i
/\  fp_w (W_VAR s _)		= prime 5 s
/\  fp_w (W_EXPLICIT_VAR s _)	= prime 6 s
/\  fp_w (W_AND a b)		= prime 7 (fp_w a + fp_w b)
/\  fp_w (W_OR a b)		= prime 8 (fp_w a + fp_w b)
/\  fp_w (W_NOT a)		= prime 9 (fp_w a)
/\  fp_w (W_EQ a b)		= prime 10 (fp_w a + fp_w b)
/\  fp_w (W_PRED _ a)		= prime 11 (fp_w a)
/\  fp_w (W_GR a b)		= prime 12 (fp_w a) + prime 13 (fp_w b)
/\  fp_w (W_ADD a b)		= prime 14 (fp_w a + fp_w b)
/\  fp_w (W_SUB a b)		= prime 15 (fp_w a) + prime 16 (fp_w b)
/\  fp_w (W_MUL a b)		= prime 17 (fp_w a) + prime 18 (fp_w b)
/\  fp_w (W_DIV a b)		= prime 19 (fp_w a) + prime 20 (fp_w b)
/\  fp_w (W_MOD a b)		= prime 21 (fp_w a) + prime 22 (fp_w b)
/\  fp_w (W_SHL a b)		= prime 23 (fp_w a) + prime 24 (fp_w b)
/\  fp_w (W_SHR a b)		= prime 25 (fp_w a) + prime 26 (fp_w b)
/\  fp_w (W_ASHR a b)		= prime 27 (fp_w a) + prime 28 (fp_w b)
/\  fp_w (W_SX s a)		= prime 29 s + prime 30 (fp_w a)
/\  fp_w (W_ZX s a)		= prime 31 s + prime 32 (fp_w a)
/\  fp_w (W_ITE a b c)          = prime 33 (fp_w a) + prime 34 (fp_w b)
                                                    + prime 35 (fp_w c)
/\  fp_w (W_SLICE ix a)	        = prime 36 (fp_w a) + prime_list id 0 ix
/\  fp_w (W_NAMED_SLICE _ ix a) = prime 37 (fp_w a) + prime_list id 0 ix
/\  fp_w (W_UPDATE_NAMED_SLICE a _ ix b)
                                = prime 38 (fp_w a) + prime 39 (fp_w b)
                                                    + prime_list id 0 ix
/\  fp_w (W_CAT as)             = prime_list fp_w 40 as
/\  fp_w (W_MEM_READ m a b)     = prime 41 (fp_mem m) + prime 42 (fp_w a)
                                                      + prime 43 (fp_w b)
/\  fp_w (W_MEM_WRITE m a b d)  = prime 44 (fp_mem m) + prime 45 (fp_w a)
                                                      + prime 46 (fp_w b)
                                                      + prime 47 (fp_w d)
;

// Gen. a fingerprint for 'UPDATE_FN'.
let
    fp_fn (W_UPDATE_FN l r)   = prime 48 (fp_w l) + prime 49 (fp_w r)
/\  fp_fn (W_PHASE_DELAY l r) = prime 50 (fp_w l) + prime 51 (fp_w r)
;

// Gen. a fingerprint for 'CONTENT'.
let
    fp_c (P_HIER cs) = prime 51 (length cs) + prime_list fp_of 0 cs
/\  fp_c (P_LEAF fs) = prime 52 (length fs) + prime_list fp_fn 0 fs
;

// Gen. a fingerprint for a 'VECTOR'.
let
    fp_v (CONSTANT ix) = prime 54 ix
/\  fp_v (VARIABLE _)  = prime 55 1
/\  fp_v (VECTOR _ ix) = prime_list id 56 ix
;

// Gen. a fingerprint for a input/output declaration. ('str2int'?)
let
    fp_wl acs = prime_list (prime_list fp_v 0) 57 (map snd (parse_actuals acs))
;

// Gen. a fingerprint for each 'PEXLIF' on creation (fingerprint 'c'?).
let PINST name attrs leaf fa_inps fa_outs internals content =
        let new = fp_wl fa_inps + fp_wl fa_outs in
        let old = find_fingerprint attrs != 0 in
        let as' = old => (("fingerprint", int2str new) : attrs) | attrs in
        PINST name as' leaf fa_inps fa_outs internals content
;

//------------------------------------------------------------------------------

// "entity" decl. of a 'PEXLIF'.
lettype scheme =
        SCHEME
            {name::string}
            {fp::int}
            {inps::(string#(vector list)) list}
            {outs::(string#(vector list)) list}
;

// "Smart" constructor for a 'SCHEME'.
let scheme name attrs fa_inps fa_outs =
        SCHEME name (find_fingerprint attrs)
                    (parse_actuals fa_inps)
                    (parse_actuals fa_outs)
;

// Fetch the fingerprint of a 'SCHEME'.
//  fingerprint_of::scheme->int
let fingerprint_of (SCHEME _ fp _ _) = fp;

// Flatten a 'PEXLIF' into its 'SCHEME's.
//  flatten::pexlif->(scheme list)
letrec
    flatten (PINST name attrs _ fa_inps fa_outs _ (P_LEAF _)) =
        [scheme name attrs fa_inps fa_outs]
/\  flatten (PINST name attrs _ fa_inps fa_outs _ (P_HIER c)) =
        (scheme name attrs fa_inps fa_outs) : flatmap flatten c
;

// Check if two 'SCHEME's share a wire.
//  connected::scheme->scheme->bool
let connected (SCHEME name _ fa_inps fa_outs) (SCHEME name' _ fa_inps' fa_outs') =
        let shares_wire xs ys = any (\x. any (vec_eq x) ys) xs in
        let wires xs = flatmap snd xs in
        name == name' OR
        shares_wire (wires fa_inps) (wires fa_outs') OR
        shares_wire (wires fa_outs) (wires fa_inps')
;

// Construct an adjacency matrix for a 'PEXLIF' (order nodes by connectivity).
//  adjacency_matrix::pexlif->(int mat)
let adjacency_matrix pex =
        let adj x ys = map (\y. connected x y => fingerprint_of y | 0) ys in
        val scs = flatten pex then
        map (flip adj scs) scs
;

// ...
//  isomatch_matrix::pexlif->pexlif->((bool mat)#(bool mat)#(bool mat))
let isomatch_matrix piece puzzle =
        let build pi pu = map (\p. map (\q. p == q) pu) pi in
        let strip mat   = map (\c. map (\v. v != 0) c) mat in
        val pi = adjacency_matrix piece  then
        val pu = adjacency_matrix puzzle then
        val m  = build pi pu then
        (strip pi, strip pu, m)
;

//------------------------------------------------------------------------------
// Ullmanâ€™s Subgraph Isomorphism Algorithm:
//   G - graph, or puzzle.
//   P - potential subgraphs of G, or piece.
//   M - |V(P)| x |V(G)| matrix that encodes possible subgraph isomorphisms.
//------------------------------------------------------------------------------

letrec
    ix_cols _ _ p []     = []
/\  ix_cols r d p (c:cs) = p c =>
        (r,d) : ix_cols r (d+1) p cs | ix_cols r (d+1) p cs
;

letrec
    ix_rows _ p []     = []
/\  ix_rows d p (r:rs) = ix_cols d 0 p r @ ix_rows (d+1) p rs
;

// Iterate over every mat. index that is a possible match.
let forall_ones mat f = val ixs = ix_rows 0 id mat then sitlist f ixs;

// Iterate over every index in a col. that is a possible match.
let forall_neighbors i mat f = val ixs = ix_cols i 0 id (el i mat) then sitlist f (map snd ixs);

// Thin the possible set of isomorphisms.
//
// prune(...)
// do
//   for all (i,j) where M is 1
//     for all neighbors x of vi in P
//       if there is no neighbor y of vj s.t. M(x,y)=1
//         M(i,j)=0
// while M was changed
//
// prune::(bool mat)->(bool mat)->(bool mat)->(bool mat)
letrec
    prune mat g p =
      val mat' =
          forall_ones mat (\(i,j). \mat.
            forall_neighbors i p (\vi. \mat.
              forall_neighbors j g (\vj. const T) F
                => mat | mat_update F (i,j) mat
            ) mat
          ) mat
      then mat == mat' => mat' | prune mat' g p
;

//------------------------------------------------------------------------------

// "Dot-product" of two lists of booleans.
//  dot::(bool list)->(bool list)->bool
let dot xs ys = itlist (\z. \b. z OR b) (map2 (\x. \y. x AND y) xs ys) F;

// "Matrix multiplication" of two boolean matrices.
//  mult::(bool mat)->(bool mat)->(bool mat)
let mult m n = val n' = transpose n then map (\r. map (dot r) n') m;

// Iterate over all unused row-indices.
//  forall_unused::(bool list)->(int->a->a)->a->a
let forall_unused row f = val ixs = ix_cols 0 0 NOT row then sitlist f (map snd ixs);

// Mark every el. as 'F', except the 'n'th el. that is marked as 'T'.
//  mark_one::int->(bool list)->(bool list)
let mark_one n xs = modify (const T) n (map (const F) xs);

// Check that wether 'm' is a valid morphism for puzzle 'g' and piece 'p'.
//  isomorphism::(bool mat)->(bool mat)->(bool mat)->bool
let isomorphism mat g p =
        let only_one_solution mat =
            val (rs,cs) = unzip (ix_rows 0 id mat) then
            let unique xs = setify xs == xs in
            unique rs AND unique cs
        in
        only_one_solution mat AND p == mult mat (transpose (mult mat g))
;

// Find ...
//
// recurse(used_columns, cur_row, G, P, M)
//   if cur_row = num_rows(M)
//     if M is an isomorphism:
//        output yes and end the algorithm
//   M' = M
//   prune(M')
//   for all unused columns c
//     set column c in M' to 1 and other columns to 0
//       -- 'c' at 'cur_row'? I might have missed that part during translation.
//     used_columns' = used_columns
//     mark c as used in used_columns'
//     recurse(used_column', cur_row+1, G, P, M')
//   output no
//
// recurse::(bool mat)->(bool mat)->(bool mat)->(int list)->int->(bool#(bool mat))
letrec
    recurse mat g p cols row =
        row == length mat AND isomorphism mat g p => (T,mat)
          | val mat' = prune mat g p then
            forall_unused cols (\col. \(r,m). r => (r,m) |
                let m'    = modify (mark_one col) row m in
                let cols' = modify (const T) col cols in
                recurse m' g p cols' (row+1)
            ) (F,mat')
;

//------------------------------------------------------------------------------
// Fin.

// all NOT [T,T] = F;
// any NOT [F,T] = T;
// equal T T;
// const T F = T;
// flip const T F = F;
// curry const (T,F) = T;
// uncurry id T F = (T,F);
// first NOT (T,T) = (F,T);
// second NOT (T,T) = (T,F);
// butfirst [F,T] = [T];
// firstn 3 (iterate NOT T) = [T,F,T];
// ixlist (\a.\i.\b.(i>a):b) [2,2,2,2] [] = [F,F,T,T];
// modify NOT 2 [T,T,T] = [T,F,T];
// string_butfirstn "abc" 2 = "c";
// string_butfirst "abc" = "bc";
// string_butlast "abc" = "ab";
// list_update T 2 [F,F,F] = [F,T,F];
// mat_update T (2,2) [[F,F,F],[F,F,F],[F,F,F]] = [[F,F,F],[F,T,F],[F,F,F]];
// let test_vec = VECTOR "a" [1,2,4];
// vec_eq test_vec test_vec;
// parse_actual "a[1:2,4]" = test_vec;
// dot [T,F] [F,F] = F;
// mult [[T,T],[F,F]] [[F,F],[T,T]] = [[T,T],[F,F]];
// mark_one 2 [T,T,T] = [F,T,F];

//------------------------------------------------------------------------------
